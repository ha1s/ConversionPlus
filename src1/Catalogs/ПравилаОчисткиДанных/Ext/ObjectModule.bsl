
Процедура СгенерироватьУникальныйКод() Экспорт
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
		
	Если ПустаяСтрока(Код) Тогда
		Код = глИмяОбъекта(ЭтотОбъект.ОбъектВыборки);
	КонецЕсли;
	Код = СформироватьУникальныйКодДляСправочника("ПравилаОчисткиДанных", Код, Ссылка, Владелец);		
		
КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	Если НЕ ЭтоГруппа Тогда
		ТипОбъектаВыборки = Строка(ОбъектВыборки);
	КонецЕсли;	
	
    Если ПустаяСтрока(Код) Тогда
		СгенерироватьУникальныйКод();
	КонецЕсли;
	
	// Проверка корректности имени правила (кода)
	Если Не глИмяКорректно(Код) Тогда
		СообщитьОбОшибке("Не правильно указано имя правила!", Отказ);
		Возврат;
	КонецЕсли;
	
	ПроверитьНаименование();
	
	Если Порядок = 0 Тогда
		Порядок = ОчереднойПорядок("ПравилаОчисткиДанных", Родитель, Владелец);
	КонецЕсли;		
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(Основание)

	Если ТипЗнч(Основание) = Тип("СправочникСсылка.ПравилаКонвертацииОбъектов") Тогда

		Владелец           = Основание.Владелец;
		Код                = Основание.Код;
		Наименование       = Основание.Код;
		ОбъектВыборки      = Основание.Приемник;
		СпособОтбораДанных = Перечисления.СпособыОтбораДанных.СтандартнаяВыборка;

	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверитьНаименование();
	
КонецПроцедуры

Процедура ПроверитьНаименование()
	
	Если ПустаяСтрока(Наименование) Тогда
		Если ЭтоГруппа Тогда
			Наименование = Код;
		Иначе
			Если ЗначениеЗаполнено(ЭтотОбъект.ОбъектВыборки.Синоним) Тогда
				Наименование = ЭтотОбъект.ОбъектВыборки.Синоним;
			Иначе
				Наименование = Код;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
