
Перем мФормаПараметров;                 // форма параметров

Перем мРезЗапроса;

// Получает текст запроса из текстового поля
//
// Параметры:
//  СВыделением - признак получения только выделенного текста.
//
// Возвращаемое значение:
//	Текст запроса в виде строки.
//
Функция ПолучитьТекстЗапроса(СВыделением)

	Если Не СВыделением Тогда
		Возврат ЭлементыФормы.ТекстЗапроса.ПолучитьТекст();
	КонецЕсли;

    ТекстЗап = ЭлементыФормы.ТекстЗапроса.ПолучитьВыделенныйТекст();
	Если СтрДлина(ТекстЗап) <> 0 Тогда
		Возврат ТекстЗап;
	Иначе
		Возврат ЭлементыФормы.ТекстЗапроса.ПолучитьТекст();
	КонецЕсли;

КонецФункции // ПолучитьТекстЗапроса()

// Загружает результат запроса в таблицу или сводную таблицу
//
// Параметры:
//  Нет.
//
Процедура ЗагрузитьРезультат()
	
	Если мРезЗапроса <> Неопределено Тогда
		
		РезультатТаблица = мРезЗапроса.Выгрузить(ОбходРезультатаЗапроса.Прямой);
		ЭлементыФормы.ТаблицаРезультата.СоздатьКолонки();
				
	КонецЕсли;
		
КонецПроцедуры // ЗагрузитьРезультат()


Процедура КнопкаОКНажатие(Кнопка)
	
	КолонкаСсылки = РезультатТаблица.Колонки.Найти("Ссылка");
	
	Если КолонкаСсылки = Неопределено Тогда
		
		Предупреждение("Не найдена колонка ""Ссылка""");
		Возврат;
		
	КонецЕсли;
	
	Закрыть(Истина);
	
КонецПроцедуры

// выполнить запрос
Процедура КоманднаяПанель1Выполнить(Кнопка)
	
	ОбъектЗапрос = Новый Запрос;

	Для каждого СтрокаПараметров Из мФормаПараметров.Параметры Цикл
		Если СтрокаПараметров.ЭтоВыражение Тогда
			ОбъектЗапрос.УстановитьПараметр(СтрокаПараметров.ИмяПараметра, Вычислить(СтрокаПараметров.ЗначениеПараметра));
		Иначе
			ОбъектЗапрос.УстановитьПараметр(СтрокаПараметров.ИмяПараметра, СтрокаПараметров.ЗначениеПараметра);
		КонецЕсли;
	КонецЦикла;

	ОбъектЗапрос.Текст = СтрЗаменить(ПолучитьТекстЗапроса(Истина), "|", "");

	Если ПустаяСтрока(ОбъектЗапрос.Текст) Тогда
		Предупреждение("Не заполнен текст запроса!", 30);
		Возврат;
	КонецЕсли;

	мРезЗапроса = ОбъектЗапрос.Выполнить();

	ЗагрузитьРезультат();
	
КонецПроцедуры

// Обработчик нажатия кнопки командной панели "Параметры"
//
Процедура Параметры()

	ТекстЗапроса = СтрЗаменить(ПолучитьТекстЗапроса(Истина), "|", ""); 
	
	Если ПустаяСтрока(ТекстЗапроса) Тогда
		Предупреждение("Не заполнен текст запроса!", 30);
		Возврат;
	КонецЕсли;
	
	Если мФормаПараметров.Открыта() = Истина Тогда
		мФормаПараметров.Закрыть();
	Иначе
		мФормаПараметров.Открыть();
	КонецЕсли;

КонецПроцедуры // Параметры()

Процедура КоманднаяПанель1Очистить(Кнопка)
	
	мРезЗапроса = Неопределено;
	
	РезультатТаблица.Очистить();	
	
КонецПроцедуры


мФормаПараметров = ЭтотОбъект.ПолучитьФорму("ФормаПараметров", ЭтаФорма);
мРезЗапроса = Неопределено;