
Перем мЗагрузкаДанных Экспорт;
Перем РедактированиеЭлементаВФорме Экспорт;


Процедура СгенерироватьУникальныйКод() Экспорт
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
		
	Если ПустаяСтрока(Код) Тогда
		Код = глИмяОбъекта(ЭтотОбъект.Приемник);
	КонецЕсли;
	
	Код = СформироватьУникальныйКодДляСправочника("ПравилаКонвертацииОбъектов", Код, Ссылка, Владелец);		
		
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
		
		ТипИсточника = Строка(Источник);
		ТипПриемника = Строка(Приемник);
		
	КонецЕсли;
	
	Если НЕ мЗагрузкаДанных
		И НЕ ЭтоГруппа
		И ЭтоНовый() Тогда
		
		Если Не РедактированиеЭлементаВФорме Тогда
			
			// определим сохранять загруженные объекты или нет
			// сохранять если источник и приемник ссылочного типа, если и тот и другой не ссылочного - не сохранять
			ИсточникСсылка = (ЗначениеЗаполнено(Источник)) И глЕстьСсылка(Источник);
			ПриемникСсылка = (ЗначениеЗаполнено(Приемник)) И глЕстьСсылка(Приемник);
			
			Если НЕ ИсточникСсылка
				И Не ПриемникСсылка Тогда
				
				// нет смысла запоминать выгруженные объекты
				НеЗапоминатьВыгруженные = Истина;	
				
			КонецЕсли;
			
		КонецЕсли;
				
		Если Родитель.Пустая() 
			И НЕ ПустаяСтрока(ТипПриемника) Тогда
			
			УстановитьГруппуПоУмолчаниюДляПКО(ЭтотОбъект);			
						
		КонецЕсли;		
				
	КонецЕсли;
	
    Если Не мЗагрузкаДанных И 
		Не ЭтоГруппа Тогда
        
        Если Приемник.Пустая() Тогда
    		СообщитьОбОшибке("Объект-приемник не может быть пустым!", Отказ);
    		Возврат;
        КонецЕсли; 
        
        Если ПустаяСтрока(Код) Тогда
    		СгенерироватьУникальныйКод();
    	КонецЕсли;

        Если ПустаяСтрока(Наименование) Тогда
    		Наименование = глНаименованиеПКО(ЭтотОбъект);
    	КонецЕсли; 
    	
    	Если Порядок = 0 Тогда
    		Порядок = ОчереднойПорядок("ПравилаКонвертацииОбъектов", Родитель, Владелец);
    	КонецЕсли;
        
    	// Проверка корректности имени правила (кода)
        Если Не глИмяКорректно(Код) Тогда
    		СообщитьОбОшибке("Не правильно указано имя правила!", Отказ);
    		Возврат;
        КонецЕсли;        
        
        // По умолчанию будем синхронизировать ссылочные объекты по
        // внутренним идентификаторам, если обе конфигурации на плафторме 8.0
		Если ЭтоПриложение8(Владелец.Приемник.Приложение) И
			 ЭтоПриложение8(Владелец.Источник.Приложение) Тогда
			 
			ЭтоПеречисление = Ложь;
			ЕстьСсылка = глЕстьСсылка(Приемник, ЭтоПеречисление);
						
			Если ЭтоНовый()
				И НЕ РедактированиеЭлементаВФорме
				И ЕстьСсылка 
				И НЕ ЭтоПеречисление Тогда
				
				СинхронизироватьПоИдентификатору = Истина;
				
			КонецЕсли;
			
			Если (Не ЕстьСсылка) И СинхронизироватьПоИдентификатору Тогда
				
				СинхронизироватьПоИдентификатору = Ложь;
				Сообщить("По идентификаторам могут синхронизироваться только объекты ссылочных типов!
						  |Синхронизация по идентификаторам ОТКЛЮЧЕНА!");
						  
			КонецЕсли;
					  
		Иначе
			// для 7.7 нет синхронизации по внутренним идентификаторам
			СинхронизироватьПоИдентификатору = ЛОЖЬ;
			ИспользоватьБыстрыйПоискПриЗагрузке = Ложь;
			ВыгружатьОбъектТолькоПриНаличииНаНегоСсылки = Ложь;
			НеЗамещатьОбъектСозданныйВИнформационнойБазеПриемнике = Ложь;
						
		КонецЕсли;
		
		Если Не ЭтоПриложение8(Владелец.Приемник.Приложение) Тогда
		
			ГенерироватьНовыйНомерИлиКодЕслиНеУказан = Ложь;
			
		КонецЕсли;
		
		Если НЕ СинхронизироватьПоИдентификатору Тогда
			
			ПродолжитьПоискПоПолямПоискаЕслиПоИдентификаторуНеНашли = Ложь;
			ПриПереносеОбъектаПоСсылкеУстанавливатьТолькоGIUD = Ложь;
			
		КонецЕсли;
				
	ИначеЕсли ЭтоГруппа Тогда
		
        Если ПустаяСтрока(Код) И (Не ПустаяСтрока(Наименование)) Тогда
    		Наименование = Код;
		КонецЕсли;
		
        Если ПустаяСтрока(Наименование) И (Не ПустаяСтрока(Код)) Тогда
    		Код = Наименование;
		КонецЕсли;
		
    	Если Порядок = 0 Тогда
    		Порядок = ОчереднойПорядок("ПравилаКонвертацииОбъектов", Родитель, Владелец);
		КонецЕсли; 
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ПустаяСтрока(Наименование) Тогда
		
		Наименование = глНаименованиеПКО(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

мЗагрузкаДанных = Ложь;
РедактированиеЭлементаВФорме = Ложь;
